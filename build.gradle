/*
 * Copyright 2016 Dgraph Labs, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.4'
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'base'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.5'
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'com.github.sherter.google-java-format'
apply plugin: 'signing'

group = 'io.dgraph'
version = '24.1.1'

base {
    archivesName = 'dgraph4j'
}

// Upgrade to Java SE 11 to match updated protobuf-protoc version.
// WARNING: Needs local toolchain upgrade from Java 8 to Java 11.
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

def grpcVersion = '1.69.0'
def dgraph4jVersion = "$version"
def openCensusVersion = '0.31.1'

protobuf {
    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = 'com.google.protobuf:protoc:4.29.1'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'annotate_code'
                }
            }
        }
        all()*.plugins {
            grpc { }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

repositories {
    mavenCentral()
}

// configurations for the integration test
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    implementation 'org.slf4j:slf4j-api:2.0.16'

    // Specific slf4j bindings should not be included in the default (compile) scope.
    // See http://www.slf4j.org/faq.html#maven2.
    testImplementation 'org.slf4j:slf4j-simple:2.0.16'

    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    implementation 'com.google.guava:guava:33.3.1-jre'

    // Explicit matching of protobuf-java and protoc versions to address
    // compilation errors arising due to transitive dependency on the version of
    // protobuf-java that grpc depends on.
    implementation 'com.google.protobuf:protobuf-java:4.29.1'

    testImplementation "io.opencensus:opencensus-api:${openCensusVersion}"
    testImplementation "io.opencensus:opencensus-exporter-trace-jaeger:${openCensusVersion}"
    testRuntimeOnly "io.opencensus:opencensus-impl:${openCensusVersion}"

    // Used for unmarshalling a JSON GraphQL response
    testImplementation 'com.google.code.gson:gson:2.11.0'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    testImplementation 'org.testng:testng:6.8.8'

    // javax.annotation is removed from the oracle java se 11, and requires explicit dependency
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

googleJavaFormat {
    toolVersion = '1.5'
    exclude 'src/generated/**/*.java'
}
compileJava.dependsOn 'googleJavaFormat'

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java")
        sourceDirs += file("${projectDir}/src/generated/main/grpc")
    }
}

test {
    useTestNG()
    //testLogging.showStandardStreams = true
    systemProperties['org.slf4j.simpleLogger.log.io.dgraph.DgraphIntegrationTest'] = 'DEBUG'
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Dgraph Client for Java',
                   'Implementation-Version': version
    }
    base { archivesName = project.name + '-all' }
    from { configurations.compile.collect { it.directory() ? it : zipTree(it) } }
    with jar
}

tasks.named('shadowJar', ShadowJar) {
    enableRelocation true
    relocationPrefix 'io.dgraph.dgraph4j.shaded'
    relocate 'google', 'io.dgraph.dgraph4j.shaded.google'
    archiveClassifier.set('shaded')
    mergeServiceFiles()
}

task buildJar(type: Jar)

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task updateProto(type: Exec) {
    workingDir 'src/main/proto'
    commandLine 'wget', '-O', 'api.proto', 'https://raw.githubusercontent.com/dgraph-io/dgo/master/protos/api.proto'
}

task setupCluster(type: Exec) {
    workingDir System.getenv('GOPATH') + '/src/github.com/dgraph-io/dgraph/dgraph'

    commandLine './run.sh'
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

artifacts {
    archives jar
    archives shadowJar
    archives sourceJar
    archives javadocJar
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

if (project.hasProperty('ossrhUsername')) {
    signing {
        sign configurations.archives
    }

    publishing {
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }

                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }

                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = 'dgraph4j'
                    description = 'Dgraph Java Client'
                    url = 'https://github.com/dgraph-io/dgraph4j'

                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'Dgraph Labs'
                            email = 'helo@hypermode.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/dgraph-io/dgraph4j.git'
                        url = 'https://github.com/dgraph-io/dgraph4j'
                    }
                }
            }
        }
    }
}

task version {
    doLast {
        println dgraph4jVersion
    }
}
